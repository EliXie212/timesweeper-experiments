IDS, = glob_wildcards("vcfs/raw/{id}.vcf.gz")

rule all:
    input:
        "vcfs/merged/ts_merged_0s.calls.vcf.gz",
        "vcfs/merged/ts_merged_missing.filtered.calls.vcf.gz"

rule call_vars:
    input:
        "vcfs/raw/{id}.vcf.gz"
    output:
        "vcfs/calls/{id}.called.vcf.gz"
    threads: 4
    shell:
        """
        bcftools call --threads {threads} -m -Oz {input} > {output}
        """

rule index:
    input:
        "vcfs/calls/{id}.called.vcf.gz"
    output:
        "vcfs/calls/{id}.called.vcf.gz.csi"
    threads: 2
    shell:
        """
        bcftools index --threads {threads} {input}
        """

rule merge_0s:
    input:
        vcfs = expand("vcfs/calls/{id}.called.vcf.gz", id=IDS),
        indexes = expand("vcfs/calls/{id}.called.vcf.gz.csi", id=IDS),
    output:
        "vcfs/merged/ts_merged_0s.calls.vcf.gz"
    threads: workflow.cores
    shell:
        """
        bcftools merge -Oz --threads {threads} --force-samples -0 {input.vcfs} > {output}
        """

rule merge_missing:
    input:
        vcfs = expand("vcfs/calls/{id}.called.vcf.gz", id=IDS),
        indexes = expand("vcfs/calls/{id}.called.vcf.gz.csi", id=IDS),
    output:
        "vcfs/merged/ts_merged_missing.calls.vcf.gz"
    threads: workflow.cores
    shell:
        """
        bcftools merge -Oz --threads {threads} --force-samples {input.vcfs} > {output}
        """

rule filter:
    input:
        "vcfs/merged/ts_merged_missing.calls.vcf.gz"
    output:
        "vcfs/merged/ts_merged_missing.filtered.calls.vcf.gz"
    threads: workflow.cores
    shell:
        """
        bcftools view -Oz -i 'F_MISSING<0.2' {input} > {output}
        """